/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0' apply false
    id "com.diffplug.spotless" version "5.14.2" apply false
    id "net.ltgt.errorprone" version "2.0.2" apply false
}

description = "Flink Demos"

allprojects {
    apply plugin: 'idea'
    group = 'org.apache.dmvk'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: "net.ltgt.errorprone"

    ext {
        flinkVersion = '1.17-SNAPSHOT'
        lombokVersion = '1.18.22'
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {

        if (project.name != "beam") {
            compileOnly "org.apache.flink:flink-streaming-java:${flinkVersion}"
            compileOnly "org.apache.flink:flink-table-api-java-bridge:${flinkVersion}"
            testImplementation "org.apache.flink:flink-streaming-java:${flinkVersion}"
            testImplementation("org.junit.jupiter:junit-jupiter:5.8.2")
        }

        // error prone
        errorprone "com.uber.nullaway:nullaway:0.9.5"
        errorprone "com.google.errorprone:error_prone_core:2.11.0"

        implementation "com.google.guava:guava:31.1-jre"

        // lombok
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }

    tasks.withType(JavaCompile).configureEach {
        options.errorprone {
            error("MissingOverride", "NullAway")
            option("NullAway:AnnotatedPackages", "org.apache.dmvk")
        }
    }

    spotless {
        java {
            googleJavaFormat('1.14.0')
        }
    }

    shadowJar {
        mergeServiceFiles()
        zip64(true)
    }

    test {
        useJUnitPlatform()
    }
}
